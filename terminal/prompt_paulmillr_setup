#
# My simple terminal theme.
#
# dotfiles ❯ (default)
# dotfiles git:master ❯ (in git repository)
# root@serv dotfiles git:master ❯ (with SSH)
#
# Authors:
#   Paul Miller
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

function prompt_paulmillr_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_paulmillr_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_paulmillr_precmd

  zstyle ':omz:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':omz:module:editor:keymap:primary' overwrite ' %F{red}♺%f'
  zstyle ':omz:module:editor:keymap' alternate ' %F{yellow}❮%f%B%F{red}❮%f%b%F{red}❮%f'
  zstyle ':omz:module:git' action ':%%B%F{yellow}%s%f%%b'
  zstyle ':omz:module:git' added ' %%B%F{green}✚%f%%b'
  zstyle ':omz:module:git' ahead ' %%B%F{yellow}⬆%f%%b'
  zstyle ':omz:module:git' behind ' %%B%F{yellow}⬇%f%%b'
  zstyle ':omz:module:git' branch ':%F{red}%b%f'
  zstyle ':omz:module:git' commit ':%F{green}%.7c%f'
  zstyle ':omz:module:git' deleted ' %%B%F{red}✖%f%%b'
  zstyle ':omz:module:git' modified ' %%B%F{blue}✱%f%%b'
  zstyle ':omz:module:git' position ':%F{green}%p%f'
  zstyle ':omz:module:git' renamed ' %%B%F{magenta}➜%f%%b'
  zstyle ':omz:module:git' stashed ' %%B%F{cyan}✭%f%%b'
  zstyle ':omz:module:git' unmerged ' %%B%F{yellow}═%f%%b'
  zstyle ':omz:module:git' untracked ' %%B%F{white}◼%f%%b'
  zstyle ':omz:module:git' info \
    'prompt'  ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  PROMPT='%F{magenta}${SSH_TTY:+%n@%m }%F{cyan}%1~%f${git_info:+${(e)git_info[prompt]}} %(!.%B%F{red}#%f%b.%B%F{green}❯%f%b) '
  RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_paulmillr_setup "$@"

